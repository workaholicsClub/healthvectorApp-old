apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    applicationVariants.all { variant -> renameArtifact(variant, defaultConfig) }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    defaultConfig {
        applicationId "ru.android.childdiary"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 10
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "int", "DB_VERSION", "1"
        buildConfigField "String", "DB_NAME", "\"diary\""
        resValue "string", "file_provider_authorities", "ru.android.childdiary"
        resValue "string", "font_path_regular", "fonts/Roboto-Regular.ttf"
        resValue "string", "font_path_bold", "fonts/Roboto-Bold.ttf"
        resValue "string", "font_path_medium", "fonts/Roboto-Medium.ttf"
        multiDexEnabled true
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles 'proguard-rules.pro'
        }
    }
}

def renameArtifact(variant, defaultConfig) {
    variant.outputs.each { output ->
        def formattedDate = new Date().format("yyyy.MM.dd")
        def fileName = applicationName + "_" + formattedDate + "_v" + defaultConfig.versionName + "(" + defaultConfig.versionCode + ")_" + variant.name + ".apk"

        def file = output.packageApplication.outputFile
        output.packageApplication.outputFile = new File(file.parent, fileName)
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.android.support:multidex:1.0.1"

    compile "org.slf4j:slf4j-api:1.7.22"

    def logbackVersion = '1.1.1-6'
    compile "com.github.tony19:logback-android-core:$logbackVersion"
    compile("com.github.tony19:logback-android-classic:$logbackVersion") {
        exclude group: 'com.google.android', module: 'android'
    }

    androidTestCompile("com.android.support.test.espresso:espresso-core:2.2.2", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile "junit:junit:4.12"

    def androidSupportVersion = '25.2.0'
    compile "com.android.support:support-v4:$androidSupportVersion"
    compile "com.android.support:appcompat-v7:$androidSupportVersion"
    compile "com.android.support:design:$androidSupportVersion"
    compile "com.android.support:recyclerview-v7:$androidSupportVersion"
    compile "com.android.support:cardview-v7:$androidSupportVersion"

    compile "io.reactivex.rxjava2:rxjava:2.0.5"
    compile "io.reactivex.rxjava2:rxandroid:2.0.1"

    compile "com.jakewharton.rxbinding2:rxbinding:2.0.0"

    def requeryVersion = '1.3.2'
    compile "io.requery:requery:$requeryVersion"
    compile "io.requery:requery-android:$requeryVersion"
    annotationProcessor "io.requery:requery-processor:$requeryVersion"

    compile "net.danlew:android.joda:2.9.5.1"

    def daggerVersion = '2.9'
    compile "com.google.dagger:dagger:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"

    compile "javax.annotation:jsr250-api:1.0"

    def moxyVersion = '1.4.5'
    compile "com.arello-mobile:moxy:$moxyVersion"
    compile "com.arello-mobile:moxy-android:$moxyVersion"
    compile "com.arello-mobile:moxy-app-compat:$moxyVersion"
    annotationProcessor "com.arello-mobile:moxy-compiler:$moxyVersion"

    def lombokVersion = '1.16.12'
    provided "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    def butterknifeVersion = '8.5.1'
    compile "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    def icepickVersion = '3.2.0'
    compile "frankiesardo:icepick:$icepickVersion"
    provided "frankiesardo:icepick-processor:$icepickVersion"

    compile "com.f2prateek.rx.preferences2:rx-preferences:2.0.0-RC1"

    compile "com.yalantis:ucrop:2.2.0"

    compile "com.github.paolorotolo:appintro:4.1.0"

    compile("com.mikepenz:materialdrawer:5.8.1@aar") {
        transitive = true
    }

    compile "com.wdullaer:materialdatetimepicker:3.1.1"

    // используем версию 1.3.0 -- последнюю, которая поддерживает API level 14
    compile("com.github.ozodrukh:CircularReveal:1.3.0@aar") {
        transitive = true;
    }

    compile "com.daimajia.swipelayout:library:1.2.0@aar"

    compile "uk.co.chrisjenx:calligraphy:2.2.0"
}
